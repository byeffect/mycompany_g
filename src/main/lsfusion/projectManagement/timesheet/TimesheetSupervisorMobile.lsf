MODULE TimesheetSupervisorMobile;

REQUIRE Timesheet, Mobile;

PRIORITY MasterData;

NAMESPACE ProjectManagement;

FORM timesheetSupervisorMobile 'Timesheet'
    OBJECTS d = DATE PANEL
    PROPERTIES d '' = VALUE(d)
    PROPERTIES dow '' = extractDOWName (d) READONLY

    PROPERTIES prevDate '<' = { SEEK timesheetSupervisorMobile.d = subtract(d, 1); },
               nextDate '>' = { SEEK timesheetSupervisorMobile.d = sum(d, 1); }

    PROPERTIES(d) copyApply, clearApply
    
    PROPERTIES() nameTimesheetProject, nameTimesheetTimeEntryType

    EVENTS ON INIT {
        timesheetProject() <- defaultProject(currentUser());
        timesheetTimeEntryType() <- OVERRIDE defaultTimeEntryType(), firstTimeEntryType();
    }

    OBJECTS e = Employee
    PROPERTIES(e) READONLY name
    PROPERTIES hoursTimesheet(d, e) BACKGROUND backgroundHoursTimesheet(d, e)
               READONLYIF NOT timesheetTimeEntryType()
               ON CHANGE { 
                   INPUT n = hoursTimesheet(d, e) DO
                       changeSaveTimesheetHours(d, e, timesheetTimeEntryType(), n, NULL, TRUE); 
               }
    
    OBJECTS h = TimeEntryHours
    PROPERTIES NOWAIT changeHours = { changeSaveTimesheetHours(d, e, timesheetTimeEntryType(), hours(h), h, TRUE); }
               COLUMNS (h) HEADER nameHours(h)
    FILTERS type(h) = timesheetTimeEntryType()
;

DESIGN timesheetSupervisorMobile {
    OBJECTS {
        NEW row1 {
            horizontal = TRUE;
            alignment = STRETCH;
            MOVE PROPERTY(nameTimesheetProject()) {
                fill = 1;
                caption = '';
                placeholder = 'Project';
            }
            MOVE PROPERTY(clearApply(d));
        }
        NEW row2 {
            horizontal = TRUE;
            alignment = STRETCH;
            NEW dates {
                fill = 1;
                horizontal = TRUE;
                MOVE PROPERTY(prevDate);
                MOVE PROPERTY(d);
                MOVE PROPERTY(nextDate);
                MOVE PROPERTY(dow) { width = 20; }
            }
            MOVE PROPERTY(copyApply(d));
        }
        MOVE GRID(e) {
            PROPERTY(name(e)) { caption = ''; }
            PROPERTY(hoursTimesheet(d, e)) { caption = ''; charWidth = 4; };
        }
        REMOVE BOX(e);
        MOVE PROPERTY(nameTimesheetTimeEntryType()) { 
            alignment = STRETCH;
            caption = '';
            placeholder = 'Type';
        }
        MOVE PROPERTY(changeHours) {
            alignment = STRETCH;
        }

        REMOVE TOOLBARBOX;
    }
} 

NAVIGATOR {
    dashboards {
        NEW 'Supervisor timesheet mobile' = timesheetSupervisorMobile;
    }
}

showMobileForms() + {
    IF permit(currentUser(), navigatorElementCanonicalName('ProjectManagement.timesheetSupervisorMobile')) THEN
        SHOW timesheetSupervisorMobile NOWAIT;
}