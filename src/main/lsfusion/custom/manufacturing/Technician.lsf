MODULE Technician;

REQUIRE Employee, ManufacturingSettings;

PRIORITY MasterData;

NAMESPACE Manufacturing;

CLASS Technician 'Техник' : Employee;

CLASS Qualification 'Квалификация' {
    seniorEngineer 'Ведущий инженер',
    engineer 'Инженер',
    technician 'Техник'
}

name 'Квалификация' (Qualification q) = staticCaption(q);

qualification = DATA Qualification (Technician);
nameQualification 'Квалификация ' (Technician t) = name(qualification(t));

startDate 'Дата начала работы' = DATA DATE (Technician);
WHEN SET(Technician t IS Technician) AND NOT startDate(t) DO startDate(t) <- currentDate();

experience 'Стаж работы (лет)' (Technician t) = extractYear(currentDate()) - extractYear(startDate(t));

image = DATA IMAGEFILE (Technician);
loadImage 'Загрузить фото' (Technician t)  {
    INPUT = image(t) CHANGE;
}
dropImage 'Удалить фото'(Technician t)  {
    image(t) <- NULL;
}

EXTEND FORM employee
    PROPERTIES(e) nameQualification, startDate, experience, 
                  image, loadImage, dropImage
;

DESIGN employee {
    header {
        NEW technician {
            caption = 'Техник';
            MOVE PROPERTY(nameQualification(e));
            MOVE PROPERTY(startDate(e));
            MOVE PROPERTY(experience(e));
        }
        details {
            NEW image {
                caption = 'Фото';
                MOVE PROPERTY(image(e)) {
                    fill = 1;
                    panelCaptionVertical = TRUE;
                    caption = 'Фото';
                }
                NEW imageActions {
                    horizontal = TRUE;
                    MOVE PROPERTY(loadImage(e));
                    MOVE PROPERTY(dropImage(e));
                }
            }
        }
    }
}

FORM technicians 'Техники'
    OBJECTS t = Technician
    PROPERTIES(t) READONLY firstName, lastName, nameQualification, startDate, experience
    PROPERTIES(t) NEWSESSION NEW, EDIT, DELETE
;

FORM dialogTechnicians 'Техники'
    OBJECTS t = Technician
    PROPERTIES(t) READONLY firstName, lastName, nameQualification

    LIST Technician OBJECT t
;

NAVIGATOR {
    settings {
        NEW technicians;
    }
}