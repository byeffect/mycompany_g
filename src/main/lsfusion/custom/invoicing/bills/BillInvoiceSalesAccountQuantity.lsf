MODULE BillInvoiceSalesAccountQuantity;

REQUIRE SalesLedgerAccountBillInvoice, BillCanceled;

NAMESPACE Invoicing;

quantity 'Qty' = DATA NUMERIC[16,3] (BillLine, InvoiceLine);

matchQuantity (BillLine bl, InvoiceLine sl) = item(bl) = item(sl) AND date(bl) <= date(sl) AND active(invoice(sl));

matchedQuantity (InvoiceLine il, CustomSalesAccount sa) = GROUP SUM quantity(BillLine bl, il) IF salesAccount(bl) = sa AND active(bill(bl)) MATERIALIZED;

matchedQuantity 'Matched' (BillLine bl) = GROUP SUM quantity(bl, InvoiceLine il) MATERIALIZED;
matchedQuantity 'Matched' (InvoiceLine il) = GROUP SUM quantity(BillLine bl, il) MATERIALIZED;

leftQuantity (InvoiceLine il, CustomSalesAccount sa) = (quantity(il) IF sa IS CustomSalesAccount) (-) matchedQuantity(il, sa); 

leftQuantity 'Left' (BillLine bl) = quantity(bl) (-) matchedQuantity(bl);
leftQuantity 'Left' (InvoiceLine il) = quantity(il) (-) matchedQuantity(il);

leftLines 'Не разнесено строк' (Bill b) = GROUP SUM 1 IF leftQuantity(BillLine bl) IF bill(bl) = b;

CONSTRAINT leftQuantity(BillLine bl) < 0.0 
    MESSAGE 'По строке поступления расписано количество больше, чем количество в документе';

CONSTRAINT leftQuantity(InvoiceLine il) < 0.0 AND quantity(il) >= 0.0
    MESSAGE 'По строке реализации расписано количество больше, чем количество в документе';

match 'Разнести' (BillLine bl) {
    quantity(bl, InvoiceLine il) <- NULL;
    quantity(bl, InvoiceLine il) <- PARTITION UNGROUP quantity
                                              LIMIT leftQuantity(il, salesAccount(bl)) IF matchQuantity(bl, il)
                                              ORDER dateTime(il), il
                                              BY bl;
}

match 'Разнести' (Bill b) {
    FOR bill(BillLine bl) = b INLINE DO
        match(bl);
}

matchBillInvoice (DATE d) {
    NEWSESSION {
        quantity(BillLine bl, InvoiceLine il) <- NULL WHERE date(bl) >= d AND salesAccount(bl) AND active(bill(bl));
        FOR date(Bill b) >= d AND active(b) ORDER dateTime(b), b NOINLINE DO {
            match(b);
        }
        APPLY;
    }
}

amount (BillLine bl, InvoiceLine il) += NUMERIC[16,4](quantity(bl, il) * price(bl));

EXTEND FORM bill
    PROPERTIES(sbl) READONLY matchedQuantity
    PROPERTIES(sbl) DISABLEIF NOT salesAccount(b) match GRID
    PROPERTIES(b) DISABLEIF NOT salesAccount(b) match DRAW sbl TOOLBAR
    
    OBJECTS qil = InvoiceLine
    PROPERTIES(qil) READONLY date, number, index, nameItem, quantity, matchedQuantity, price, untaxedAmount
    PROPERTIES quantity(sbl, qil), amount(sbl, qil)
    PROPERTIES(qil) EDIT
    
    FILTERS matchQuantity(sbl, qil) 
    
    FILTERGROUP matchedInvoice
        FILTER 'Matched' quantity(sbl, qil) DEFAULT
;

DESIGN bill {
    salesAccountPane {
        horizontal = TRUE;
        MOVE BOX(qil);
    }
}

EXTEND FORM bills
    PROPERTIES(b) READONLY leftLines
;