MODULE ReceiptPackageMobile;

REQUIRE ReceiptDone, ReceiptPackage, PackageLot, ReceiptScan, Mobile, Icon;

NAMESPACE Inventory;

readonlyReceipt = DATA LOCAL BOOLEAN ();

htmlReceipt '' (Receipt r) = HTML (CONCAT '&nbsp;', '<b>' + number(r) + '</b>', scheduledDate(r), '<font size="-1">' + nameVendor(r) + '</font>');

htmlReceipt '' (PackageLine l) = HTML (CONCAT '&nbsp;', '<b>' + nameProduct(l) + '</b>', '<font size="-2">' + idLot(l) + '</font>');

htmlReceipt '' (ReceiptScan l) = HTML (CONCAT '&nbsp;',
    time(l),
    '<b>' + nameProduct(l) + '</b>',
    '<font size="-2">' + idLot(l) + '</font>',
    '<i>' + referencePackage(l) + '</i>',
    '<u>' + nameLocation(l) + '</u>');

 quantityPackageLine '{Quantity}' (Package p) = GROUP SUM quantity(PackageLine l) IF package(l) = p;

// Форма приемки
FORM receiptPackage
    OBJECTS r = Receipt PANEL
    PROPERTIES(r) READONLY htmlReceipt

    OBJECTS p = Package NULL PANEL
    PROPERTIES(p) READONLY reference
    FILTERS in(r, p)

    OBJECTS s = ReceiptScan
    PROPERTIES(s) READONLY htmlReceipt
    PROPERTIES(s) READONLY nameLocation PANEL
    PROPERTIES(s) NEWSESSION deleteScan = DELETE SHOWIF NOT readonlyReceipt()
    FILTERS receipt(s) = r

    OBJECTS pc = Package
    PROPERTIES READONLY BACKGROUND IF quantityPackageLine(pc) = quantityScan(r, pc) THEN RGB(212,255,212) ELSE
                                   IF quantityScan(r, pc) THEN RGB(255,255,212)
                        reference(pc), quantityPackageLine(pc), quantityScan(r, pc) 
    FILTERS in(r, pc)

    OBJECTS pl = PackageLine
    PROPERTIES(pl) READONLY htmlReceipt BACKGROUND RGB(212,255,212) IF scan(r, lot(pl))
    FILTERS package(pl) = p
;

DESIGN receiptPackage {
    caption = badged('Приемка', number(r));
    OBJECTS {
        class = '';
        MOVE PROPERTY(htmlReceipt(r)) {
            caption = '';
            alignment = STRETCH;
            focusable = FALSE;
        }
        REMOVE BOX(r);

        MOVE PROPERTY(reference(p)) {
            caption = 'Упаковка';
            alignment = STRETCH;
            focusable = FALSE;
        }

        NEW pane {
            fill = 1;
            tabbed = TRUE;
            NEW scans {
                caption = badged('Принято', countScan(r));
                MOVE GRID(s);
                REMOVE BOX(s);
                MOVE PROPERTY(deleteScan) { alignment = STRETCH; }
            }
            NEW packageContent {
                caption = badged('Содержимое', countLines(p));
                MOVE GRID(pl);
                REMOVE BOX(pl);
            }
            NEW packages {
                caption = badged('Упаковки', countPackages(r));
                MOVE GRID(pc);
                REMOVE BOX(pc);
            }
        }
        MOVE PROPERTY(nameLocation(s)) {
            caption = 'Место хранения';
            focusable = FALSE;
        }
    }
    REMOVE TOOLBARBOX;
}

selectPackage 'Выбрать' (Package p) {
    SEEK receiptPackage.p = p;
}

EXTEND FORM receiptPackage
    PROPERTIES(pc) selectPackage
;
DESIGN receiptPackage {
    packages {
        MOVE PROPERTY(selectPackage(pc)) { alignment = STRETCH; }
    }
}

// scan
barcode = DATA LOCAL STRING () CHANGEKEY 'F4';
scanBarcode(Receipt r, Package p, ReceiptScan rs) {
    INPUT b = STRING DO {
        IF packageReference(r, b) THEN {
            SEEK receiptPackage.p = packageReference(r, b);
        } ELSE {
            IF NOT p THEN
                MESSAGE 'Не выбрана упаковка. Сначала просканируйте ее.';
            ELSE {
                IF NOT lot(b) THEN {
                    IF NOT location(b) OR NOT rs THEN
                        MESSAGE 'Не найден код партии';
                    ELSE
                        NEWSESSION APPLY { location(rs) <- location(b); }
                } ELSE {
                    IF scan(r, lot(b)) THEN
                        MESSAGE 'Код партии уже был принят';
                    ELSE {
                        IF NOT GROUP SUM 1 IF package(PackageLine pl) = p AND lot(pl) = lot(b) THEN
                            MESSAGE 'Этой партии не должно быть в текущей упаковке';
                        ELSE {
                            NEWSESSION APPLY {
                                NEW s = ReceiptScan {
                                    receipt(s) <- r;
                                    package(s) <- p;
                                    lot(s) <- lot(b);
                                    SEEK receiptPackage.s = s;
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

EXTEND FORM receiptPackage
    PROPERTIES() barcode ON CHANGE scanBarcode(r, p, s) SHOWIF NOT readonlyReceipt()
;
DESIGN receiptPackage {
    OBJECTS {
        MOVE PROPERTY(barcode()) AFTER PROPERTY(htmlReceipt(r)) {
            caption = '';
            placeholder = 'Введите штрих-код';
            alignment = STRETCH;
        }
    }
}
