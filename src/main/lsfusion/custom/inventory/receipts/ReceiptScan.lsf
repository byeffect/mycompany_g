MODULE ReceiptScan;

REQUIRE Receipt, Package, Lot, LocationProduct;

NAMESPACE Inventory;

CLASS ReceiptScan 'Сканирование на приемке';
receipt = DATA Receipt (ReceiptScan) NONULL DELETE INDEXED;

countScan 'Просканировано' (Receipt r) = GROUP SUM 1 IF receipt(ReceiptScan s) = r MATERIALIZED;

user = DATA CustomUser (ReceiptScan);
user(ReceiptScan s) <- currentUser() WHEN SET(s IS ReceiptScan);

dateTime 'Время' = DATA DATETIME (ReceiptScan);
dateTime(ReceiptScan s) <- currentDateTime() WHEN SET(s IS ReceiptScan);
time 'Время' (ReceiptScan s) = TIME(dateTime(s));

package = DATA Package (ReceiptScan) INDEXED;
referencePackage 'Упаковка' (ReceiptScan r) = reference(package(r));

lot = DATA Lot (ReceiptScan) INDEXED;
idLot (ReceiptScan l) = id(lot(l));

product (ReceiptScan l) = product(lot(l));
nameProduct (ReceiptScan l) = nameProduct(lot(l));

location = DATA Location (ReceiptScan) INDEXED;
idLocation (ReceiptScan l) = id(location(l));
nameLocation (ReceiptScan l) = name(location(l));

WHEN LOCAL SETCHANGED(lot(ReceiptScan s)) AND location(location(receipt(s)), product(s)) DO 
    location(s) <- location(location(receipt(s)), product(s));

quantity '{Quantity}' = DATA NUMERIC[16,3] (ReceiptScan);
quantity(ReceiptScan r) <- 1 WHEN SET(r IS ReceiptScan);

scan (Receipt r, Lot l) = GROUP MAX ReceiptScan rs IF receipt(rs) = r AND lot(rs) = l;

quantityScan (Receipt r, Product p) = GROUP SUM quantity(ReceiptScan s) IF receipt(s) = r AND product(s) = p;
quantityScan (ReceiptLine l) = quantityScan(receipt(l), product(l));

quantityScan (Receipt r, Product p, Location l) = GROUP SUM quantity(ReceiptScan s) IF receipt(s) = r AND product(s) = p AND location(s) = l;
quantityScan (Receipt r, Lot lt) = GROUP SUM quantity(ReceiptScan s) IF receipt(s) = r AND lot(s) = lt;
quantityScan (Receipt r, Lot lt, Location l) = GROUP SUM quantity(ReceiptScan s) IF receipt(s) = r AND lot(s) = lt AND location(s) = l;