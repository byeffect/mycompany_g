MODULE ReceiptScan;

REQUIRE Receipt, Package, Lot, LocationProduct;

NAMESPACE Inventory;

CLASS ReceiptScan 'Сканирование на приемке';
receipt = DATA Receipt (ReceiptScan) NONULL DELETE INDEXED;

countScan 'Просканировано' (Receipt r) = GROUP SUM 1 IF receipt(ReceiptScan s) = r MATERIALIZED;

user = DATA CustomUser (ReceiptScan);
nameUser 'Пользователь' (ReceiptScan s) = name(user(s));
user(ReceiptScan s) <- currentUser() WHEN SET(s IS ReceiptScan);

dateTime 'Время' = DATA DATETIME (ReceiptScan);
dateTime(ReceiptScan s) <- currentDateTime() WHEN SET(s IS ReceiptScan);
time 'Время' (ReceiptScan s) = TIME(dateTime(s));

package = DATA Package (ReceiptScan);
referencePackage 'Упаковка' (ReceiptScan r) = reference(package(r));
INDEX package(ReceiptScan s), receipt(s); 

lot = DATA Lot (ReceiptScan);
idLot 'Lot' (ReceiptScan l) = id(lot(l));
INDEX lot(ReceiptScan s), receipt(s);

product (ReceiptScan l) = product(lot(l));
nameProduct 'Product' (ReceiptScan l) = nameProduct(lot(l));

location = DATA Location (ReceiptScan);
idLocation (ReceiptScan l) = id(location(l));
nameLocation 'Location' (ReceiptScan l) = name(location(l));
INDEX location(ReceiptScan s), receipt(s), lot(s);

WHEN LOCAL SETCHANGED(lot(ReceiptScan s)) AND location(location(receipt(s)), product(s)) DO 
    location(s) <- location(location(receipt(s)), product(s));

quantity '{Quantity}' = DATA NUMERIC[16,3] (ReceiptScan);
quantity(ReceiptScan r) <- 1 WHEN SET(r IS ReceiptScan);

scan (Receipt r, Lot l) = GROUP MAX ReceiptScan rs IF receipt(rs) = r AND lot(rs) = l;

quantityScan 'Принято' (Receipt r) = GROUP SUM quantity(ReceiptScan s) IF receipt(s) = r MATERIALIZED;

quantityScan 'Принято' (Receipt r, Package p) = GROUP SUM quantity(ReceiptScan s) IF receipt(s) = r AND package(s) = p;

quantityScan (Receipt r, Product p) = GROUP SUM quantity(ReceiptScan s) IF receipt(s) = r AND product(s) = p;
quantityScan (ReceiptLine l) = quantityScan(receipt(l), product(l));

quantityScan (Receipt r, Product p, Location l) = GROUP SUM quantity(ReceiptScan s) IF receipt(s) = r AND product(s) = p AND location(s) = l;
quantityScan (Receipt r, Lot lt) = GROUP SUM quantity(ReceiptScan s) IF receipt(s) = r AND lot(s) = lt;
quantityScan (Receipt r, Lot lt, Location l) = GROUP SUM quantity(ReceiptScan s) IF receipt(s) = r AND lot(s) = lt AND location(s) = l;


EXTEND FORM receipt
    OBJECTS rsc = ReceiptScan
    PROPERTIES(rsc) READONLY nameUser, dateTime, referencePackage, idLot, nameProduct, nameLocation, quantity
    FILTERS receipt(rsc) = r
;

DESIGN receipt {
    details {
        MOVE BOX(rsc) {
            caption = badged('Сканирования', INTEGER(quantityScan(r)));
        }
    }
}
