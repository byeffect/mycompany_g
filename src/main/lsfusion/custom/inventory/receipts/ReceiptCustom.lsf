MODULE ReceiptCustom;

REQUIRE ReceiptLot, LotCustom, ReceiptPutAwayLot, ReceiptSearch, ProductModel;

NAMESPACE Inventory;

note 'Note' = DATA TEXT (ReceiptLine);

EXTEND FORM receipt
    PROPERTIES(l) note PANEL
;

DESIGN receipt {
    linesTab {
        NEW notes {
            caption = 'Примечание';
            MOVE PROPERTY(note(l)) { caption = ''; fill = 1; }
        }
    }
}

idDefaultModelProduct 'SKU' (ReceiptLine l) = idDefaultModel(product(l));

EXTEND FORM receipt
    PROPERTIES (l) READONLY idDefaultModelProduct AFTER nameProduct(l)
    PROPERTIES (itm) READONLY idDefaultModel FIRST 
;

defaultVendor = DATA Partner (ReceiptType);
nameDefaultVendor 'Vendor' (ReceiptType o) = name(defaultVendor(o));

EXTEND FORM receiptType
    PROPERTIES(o) nameDefaultVendor
;

WHEN (SETCHANGED(type(Receipt r)) OR CHANGED(vendor(r))) AND defaultVendor(type(r)) AND NOT vendor(r) DO vendor(r) <- defaultVendor(type(r));

CONSTRAINT SET(done(ReceiptLine l) (-) doneLot(l)) AND checkLot(product(l)) AND NOT skipCheckLot(location(receipt(l)))
    MESSAGE 'Количество принятых партий в приходе не соответствует принятому количеству';

CONSTRAINT SET(done(ReceiptLine l) (-) put(l)) AND checkLocation(product(l)) AND NOT skipCheckLot(location(receipt(l)))
    MESSAGE 'Количество размещенных товаров не соответствует принятому количеству';

CONSTRAINT SET(done(ReceiptLine l) (-) putLot(l)) AND checkLotLocation(product(l)) AND NOT skipCheckLot(location(receipt(l)))
    MESSAGE 'Количество размещенных партий не соответствует принятому количеству'; 