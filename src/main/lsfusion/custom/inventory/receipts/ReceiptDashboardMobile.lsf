MODULE ReceiptDashboardMobile;

REQUIRE ReceiptMobile, ReceiptPutAwayLot;

NAMESPACE Inventory;

receive 'Принять' (Receipt r) {
    SHOW receiptMobile OBJECTS r = r DOCKED NOWAIT;
} CHANGEMOUSE 'DBLCLK';

markAsDone 'Завершить' (Receipt r) {
    NEWSESSION { 
        done(r) <- TRUE;
        putAway(r) <- TRUE;
        done(ReceiptLine l) <- quantityScan(l) WHERE receipt(l) = r;
        done(ReceiptLine l, Lot lt) <- quantityScan(r, lt) IF product(l) = product(lt) WHERE receipt(l) = r;
        
        FOR NUMERIC[16,3] q = quantityScan(r, product(ReceiptLine rl), Location l) INLINE NEW p = ReceiptPutAway DO {
            receiptLine(p) <- rl;
            location(p) <- l;
            quantity(p) <- q;
            
            quantity(p, Lot lt) <- quantityScan(r, lt, l) IF product(rl) = product(lt);
        }
        
        APPLY;
    }
} CONFIRM;

open 'Открыть' (Receipt r) {
    NEWSESSION {
        readonlyReceipt() <- TRUE;
        SHOW receiptMobile OBJECTS r = r DOCKED NOWAIT;
    }
} CHANGEMOUSE 'DBLCLK';
return 'Вернуть' (Receipt r) {
    NEWSESSION { 
        done(r) <- NULL;
        putAway(r) <- NULL;
        done(ReceiptLine l) <- NULL WHERE receipt(l) = r;
        done(ReceiptLine l, Lot lt) <- NULL WHERE receipt(l) = r;
        DELETE ReceiptPutAway p WHERE receipt(receiptLine(p)) = r;
        APPLY;
    }
} CONFIRM;

FORM receiptDashboard 'Приемка'
    OBJECTS rr = Receipt
    PROPERTIES(rr) READONLY BACKGROUND IF initialDemand(rr) = quantityScan(rr) THEN RGB(212,255,212) ELSE
                                       IF quantityScan(rr) THEN RGB(255,255,212)
                            htmlReceipt, initialDemand, quantityScan
    PROPERTIES(rr) receive, markAsDone
    FILTERS status(rr) = ReceiptStatus.ready
    
    OBJECTS rd = Receipt
    PROPERTIES(rd) READONLY htmlReceipt, initialDemand, quantityScan
    PROPERTIES(rd) open, return
    FILTERS status(rd) = ReceiptStatus.done OR status(rd) = ReceiptStatus.putAway
    FILTERGROUP lastDays
        FILTER 'Последние' executionDate(rd) >= subtract(currentDate(), 1)
;

countReceiptReady = GROUP SUM 1 IF status(Receipt rr) = ReceiptStatus.ready;

DESIGN receiptDashboard {
    OBJECTS {
        class = '';
        NEW pane {
            fill = 1;
            tabbed = TRUE;
            NEW ready {
                caption = badged('В работе', countReceiptReady());
                MOVE GRID(rr) {
                    PROPERTY(htmlReceipt(rr)) { caption = ''; }
                    PROPERTY(initialDemand(rr)) { charWidth = 4; }
                    PROPERTY(quantityScan(rr)) { charWidth = 4; }
                }
                NEW readyActions {
                    alignment = STRETCH;
                    horizontal = TRUE;
                    MOVE PROPERTY(receive(rr)) { fill = 2; }
                    MOVE PROPERTY(markAsDone(rr)) { fill = 1; }
                }
                REMOVE BOX(rr);
            }
            NEW done {
                caption = 'Принятые';
                MOVE GRID(rd) {
                    PROPERTY(htmlReceipt(rd)) { caption = ''; }
                }
                NEW doneActions {
                    alignment = STRETCH;
                    horizontal = TRUE;
                    MOVE PROPERTY(open(rd)) { fill = 2; }
                    MOVE PROPERTY(return(rd)) { fill = 1; }
                }
                REMOVE BOX(rd);
            }
        }
    }
    REMOVE TOOLBARBOX;
}

NAVIGATOR {
    dashboards {
        NEW receiptDashboard;
    }
}

EXTEND FORM receiptDashboard
    PROPERTIES() barcode ON CHANGE {
        INPUT b = STRING DO
            IF NOT GROUP SUM 1 IF vendorReference(Receipt r) = b AND NOT done(r) THEN {
                beep(warningSound(), TRUE);
                MESSAGE 'Не найдено указанного номера приемки';
            } ELSE {
                FOR Receipt r = (GROUP MAX Receipt rc IF vendorReference(rc) = b AND NOT done(rc)) DO {
                    NEWSESSION APPLY { ready(r) <- TRUE; }
                    SEEK receiptDashboard.rr = r;
                    IF NOT canceled() THEN beep(infoSound(), TRUE);
                }
            }
    }
;

DESIGN receiptDashboard {
    OBJECTS {
        MOVE PROPERTY(barcode()) FIRST {
            caption = '';
            placeholder = 'Введите номер поставки';
            alignment = STRETCH;
        }
    }
}

showMobileForms() + {
    IF permit(currentUser(), navigatorElementCanonicalName('Inventory.receiptDashboard')) THEN
        SHOW receiptDashboard NOWAIT;
}