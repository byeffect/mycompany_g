MODULE LotCustom;

REQUIRE Lot, PartnerPurchase, ReceiptLot, ProductModel, InvLedgerLot;

NAMESPACE Inventory;

dataVendor = DATA Partner (Lot) INDEXED;
nameDataVendor 'Vendor' (Lot l) = name(dataVendor(l));

CONSTRAINT dataVendor(Lot l) AND NOT isVendor(dataVendor(l)) 
                CHECKED BY dataVendor[Lot]
                MESSAGE 'The partner must be a vendor';


firstReceiptLine =
        GROUP LAST ReceiptLine receiptLine
              ORDER DESC executionDateTime(receiptLine), receiptLine
              WHERE done(receiptLine, Lot l) AND active(receipt(receiptLine))
              BY l MATERIALIZED;

vendor (Lot l) = OVERRIDE dataVendor(l), vendor(firstReceiptLine(l));
nameVendor 'Vendor' (Lot l) = name(vendor(l));
sku 'SKU' (Lot l) = idDefaultModel(product(l));

EXTEND FORM lot
    PROPERTIES(l) nameVendor, sku
;

DESIGN lot {
    header {
        MOVE PROPERTY(nameVendor(l)) { alignment = STRETCH; };
        MOVE PROPERTY(sku(l)) { alignment = STRETCH; }
    }
}

EXTEND FORM lots
    PROPERTIES(l) READONLY nameVendor
;

FORM selectProduct 'Выберите товар'
    OBJECTS p = Product PANEL NULL
    PROPERTIES(p) '' = idDefaultModel SELECTOR
;

DESIGN selectProduct {
    OBJECTS {
        MOVE PROPERTY(idDefaultModel(p)) { fill = 1; }
        REMOVE PROPERTY(formRefresh());
    }
}

createLot (STRING id) {
    DIALOG selectProduct OBJECTS p INPUT DO
        IF p THEN NEWSESSION {
            NEW l = Lot {
                id(l) <- id;
                product(l) <- p;
                APPLY;
            }
        }
}

idDefaultModelProduct 'SKU' (Lot l) = idDefaultModel(product(l));

EXTEND FORM lots
    PROPERTIES (l) READONLY idDefaultModelProduct AFTER nameProduct(l)
;

order 'Заказ (производство)' = DATA STRING[100] (Lot);
assignment 'Наряд (производство)' = DATA STRING[100] (Lot);
imei2 'IMEI 2' = DATA STRING[100] (Lot) CHARWIDTH 15;
seriesNumber 'S\\N' = DATA STRING[100] (Lot) CHARWIDTH 12;

EXTEND FORM lot PROPERTIES(l) order, assignment, imei2, seriesNumber;
DESIGN lot {
    header {
        MOVE PROPERTY(order(l));
        MOVE PROPERTY(assignment(l));
        MOVE PROPERTY(imei2(l));
        MOVE PROPERTY(seriesNumber(l));
    }
}
EXTEND FORM lots PROPERTIES(l) READONLY order, assignment, imei2, seriesNumber;

checkLot 'Проверка по партиям' = DATA BOOLEAN (Category);
checkLocation 'Проверка по ячейкам' = DATA BOOLEAN (Category);
checkLotLocation 'Проверка по партиям/ячейкам' = DATA BOOLEAN (Category);

checkLotQuantity1 'Проверка на количество по партии больше 1' = DATA BOOLEAN (Category);
checkLotNegative 'Проверка на количество по партии меньше 0' = DATA BOOLEAN (Category);
checkLotOnHand 'Проверка на остаток по партии' = DATA BOOLEAN (Category);

EXTEND FORM category
    PROPERTIES(c) checkLot, checkLocation, checkLotLocation, checkLotQuantity1, checkLotNegative, checkLotOnHand
;

DESIGN category {
    tabs {
        NEW constraints {
            caption = 'Ограничения';
            MOVE PROPERTY(checkLot(c));
            MOVE PROPERTY(checkLocation(c));
            MOVE PROPERTY(checkLotLocation(c));
            MOVE PROPERTY(checkLotQuantity1(c));
            MOVE PROPERTY(checkLotNegative(c));
            MOVE PROPERTY(checkLotOnHand(c));
        }
    }
}

checkLot (Product p) = GROUP SUM 1 IF level(category(p), Category c) AND checkLot(c) MATERIALIZED;
checkLocation (Product p) = GROUP SUM 1 IF level(category(p), Category c) AND checkLocation(c) MATERIALIZED;
checkLotLocation (Product p) = GROUP SUM 1 IF level(category(p), Category c) AND checkLotLocation(c) MATERIALIZED;

checkLotQuantity (Product p) = GROUP SUM 1 IF level(category(p), Category c) AND checkLotQuantity1(c) MATERIALIZED;
CONSTRAINT SET(onHand(Lot l) > 1) AND checkLotQuantity(product(l))
    MESSAGE 'Запрещено иметь остаток по партии больше 1';

checkLotNegative (Product p) = GROUP SUM 1 IF level(category(p), Category c) AND checkLotNegative(c) MATERIALIZED;
CONSTRAINT SET(onHand(Lot l) < 0) AND checkLotNegative(product(l))
    MESSAGE 'Запрещено иметь остаток по партии меньше 0';

checkLotOnHand (Product p) = GROUP SUM 1 IF level(category(p), Category c) AND checkLotOnHand(c) MATERIALIZED;
CONSTRAINT SET(onHand(Location l, Product p) (-) onHandLot(l, p)) AND checkLotOnHand(p)
    MESSAGE 'Остаток по товару не совпадает с остатком по партии'; 
