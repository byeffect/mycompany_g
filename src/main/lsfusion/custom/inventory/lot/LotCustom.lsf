MODULE LotCustom;

REQUIRE Lot, PartnerPurchase, ReceiptLot, ProductModel;

NAMESPACE Inventory;

dataVendor = DATA Partner (Lot) INDEXED;
nameDataVendor 'Vendor' (Lot l) = name(dataVendor(l));

CONSTRAINT dataVendor(Lot l) AND NOT isVendor(dataVendor(l)) 
                CHECKED BY dataVendor[Lot]
                MESSAGE 'The partner must be a vendor';


firstReceiptLine =
        GROUP LAST ReceiptLine receiptLine
              ORDER DESC executionDateTime(receiptLine), receiptLine
              WHERE done(receiptLine, Lot l) AND active(receipt(receiptLine))
              BY l MATERIALIZED;

vendor (Lot l) = OVERRIDE dataVendor(l), vendor(firstReceiptLine(l));
nameVendor 'Vendor' (Lot l) = name(vendor(l));

EXTEND FORM lot
    PROPERTIES(l) nameVendor
;

DESIGN lot {
    header {
        MOVE PROPERTY(nameVendor(l)) { alignment = STRETCH; };
    }
}

EXTEND FORM lots
    PROPERTIES(l) READONLY nameVendor
;

FORM selectProduct 'Выберите товар'
    OBJECTS p = Product PANEL NULL
    PROPERTIES(p) '' = idDefaultModel SELECTOR
;

DESIGN selectProduct {
    OBJECTS {
        MOVE PROPERTY(idDefaultModel(p)) { fill = 1; }
        REMOVE PROPERTY(formRefresh());
    }
}

createLot (STRING id) {
    DIALOG selectProduct OBJECTS p INPUT DO
        IF p THEN NEWSESSION {
            NEW l = Lot {
                id(l) <- id;
                product(l) <- p;
                APPLY;
            }
        }
}

idDefaultModelProduct 'SKU' (Lot l) = idDefaultModel(product(l));

EXTEND FORM lots
    PROPERTIES (l) READONLY idDefaultModelProduct AFTER nameProduct(l)
;
