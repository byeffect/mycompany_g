MODULE InvoiceImportGSheets;

REQUIRE ShipmentImportGSheets, InvoiceShipment, SalesLedgerInvoiceCustom, SalesLedgerAccountInvoice, ServiceP;

NAMESPACE Invoicing;
CLASS ImportTable 'Таблица для импорта';

eStore = DATA EStore (ImportTable);
nameEStore 'Магазин' (ImportTable t) = name(eStore(t));

country = DATA Country (ImportTable);
nameCountry 'Страна' (ImportTable t) = name(country(t));
active '{Active}' = DATA BOOLEAN (ImportTable);

marketplace = DATA Marketplace (ImportTable);
nameMarketplace 'Площадка' (ImportTable t) = name(marketplace(t));
idTable 'ID таблицы' = DATA STRING (ImportTable) CHARWIDTH 40;
typeOrder 'Тип зказа' = DATA STRING (ImportTable) CHARWIDTH 20;
typeReturn  'Тип возврата' = DATA STRING (ImportTable) CHARWIDTH 20;

customerReference '{Customer_reference}' = DATA STRING[50] (InvoiceLine) INDEXED;

EXTEND FORM invoice
    PROPERTIES(l) READONLYIF readonly(i) customerReference
;

amazonInvoiceType = DATA InvoiceType ();
nameAmazonInvoiceType 'Тип реализации для Амазон' = name(amazonInvoiceType());


CLASS OtherAccount 'Прочие затраты' : CustomSalesAccount;
item = DATA Item (OtherAccount) NONULL;
nameItem 'Item' (OtherAccount a) = name(item(a));
caption = DATA STRING (OtherAccount, ImportTable);
otherAccount (STRING s, ImportTable t) = GROUP MAX OtherAccount oa BY caption(oa, t);
skip 'Не учитывать' = DATA BOOLEAN (OtherAccount);
skip(Item i) = skip(GROUP MAX OtherAccount a IF item(a) = i);

EXTEND FORM Sales.options
    OBJECTS a = OtherAccount
    PROPERTIES (a) id, name, nameItem, skip
    PROPERTIES (a) NEW, EDIT, DELETE 
;

DESIGN Sales.options{
    tabbedPane{
        NEW otherAccount{
            caption = 'Прочие затраты';
            MOVE BOX (a) {caption = ''; }
        }
    }
}


//импорт реализации с Амазон

importProducts 'Импорт товаров' (STRING idImport, STRING interval, Category category) {
    NEWSESSION {
        getValuesRange(idImport, interval, defaultAccount());
        valuesRows(INTEGER i) <- replace(replace(replace(replace(valuesRows(i), ';', ''), '[', ''), ']', ''), '",', '";');
        LOCAL str = STRING();
        str() <- GROUP CONCAT valuesRows(INTEGER i), '\n' ORDER i;
        stringToFile(str());

        LOCAL amzModel = STRING(INTEGER);
        LOCAL baseModel = STRING(INTEGER);

        IMPORT CSV ';' NOHEADER FROM resultFile() TO amzModel, baseModel;

        LOCAL item = Item (INTEGER);
        item(INTEGER i) <- productModel(baseModel(i));

        FOR [GROUP MAX INTEGER i BY amzModel(i)](STRING[100] id) AND NOT model(id) NEW m = Model DO{
            id(m) <- id;
        }
        FOR [GROUP MAX INTEGER i BY baseModel(i)](STRING[100] id) AND NOT model(id) NEW m = Model DO{
            id(m) <- id;
        }
        FOR INTEGER x = [GROUP MAX INTEGER i BY baseModel(i)](STRING id) AND Model m = model(id) AND NOT productModel(id) NEW p = Product DO{
            product(m) <- p;
            dataName(p) <- ISTRING[200](baseModel(x));
            category(p) <- category;
        }
        FOR INTEGER x = [GROUP MAX INTEGER i BY amzModel(i)](STRING[100] id) AND Model m = model(id) AND Product p = productModel(baseModel(x)) DO{
            product(m) <- p;
        }
        
        APPLY;
    }
}

categoryByName (STRING name) = GROUP MIN Category c BY name(c);

importProductsAmazon 'Импорт товаров с Амазон' (ImportTable t){
    importProducts(idTable(t), 'SKU!A2:B', categoryByName('Телефоны'));
    importProducts(idTable(t), 'SKU!D2:E', categoryByName('Здоровье'));
    importProducts(idTable(t), 'SKU!G2:H', categoryByName('Аксессуары'));
}


error 'Описание ошибки' = DATA STRING (STRING, ImportTable);
invoice (ImportTable t, DATE d) = GROUP MAX Invoice i IF date(i) == d AND type(i) == amazonInvoiceType() AND customer(i) == FBAPartner() AND eStore(i) = eStore(t) AND country(i) = country(t);
divideSymbol 'Разделитель' = DATA STRING[1] (Country);
countryDivideSymbol (ImportTable t) = divideSymbol(country(t));
dateFormat 'Формат даты' = DATA STRING (Country);
dateFormat (ImportTable t) = dateFormat(country(t));
useShortFormat 'Исп.сокращеный формат для импорта' = DATA BOOLEAN (Country);
useShortFormat 'Исп.сокращеный формат для импорта' (ImportTable t) = useShortFormat(country(t));


EXTEND FORM countries
    PROPERTIES (c) divideSymbol, dateFormat, useShortFormat
;
parseSum (STRING s, ImportTable t) = NUMERIC[14,2](replace((IF countryDivideSymbol(t) != '.'  OR NOT countryDivideSymbol(t) THEN replace(s, '.', '') ELSE s), ' ', ''));
parseSum2 (STRING s, ImportTable t) = NUMERIC[14,2](replace((IF countryDivideSymbol(t) != '.'  OR NOT countryDivideSymbol(t) THEN replace(s, '.', '') ELSE s), '\\u202f', ''));

importInvoiceAmazon 'Импорт реализации с Амазон' (ImportTable t, DATE date, BOOLEAN skipImportProducts) {
    NEWSESSION {
        error(STRING s, t) <- NULL;
        IF NOT skipImportProducts THEN  importProductsAmazon(t);

        LOCAL str = STRING();
        
        LOCAL dateTime = STRING (INTEGER);
        LOCAL dateTimeExt = DATE (INTEGER);
        LOCAL type = STRING(INTEGER);
        LOCAL reference = STRING(INTEGER);
        LOCAL referenceExt = STRING (INTEGER);
        LOCAL model = STRING(INTEGER);
        LOCAL quantity = NUMERIC[16,3](INTEGER);
        LOCAL quantityExt = NUMERIC[16,3](INTEGER);
        LOCAL productSales = NUMERIC[14,2](INTEGER);
        LOCAL productSalesStr = STRING(INTEGER);
        LOCAL productSalesTax = NUMERIC[14,2](INTEGER);
        LOCAL postageCredits = NUMERIC[14,2](INTEGER);
        LOCAL shippingCreditsTax = NUMERIC[14,2](INTEGER);
        LOCAL giftWrapCredits = NUMERIC[14,2](INTEGER);
        LOCAL giftwrapCreditsTax = NUMERIC[14,2](INTEGER);
        LOCAL promotionalRebates = NUMERIC[14,2](INTEGER);
        LOCAL promotionalRebatesTax = NUMERIC[14,2](INTEGER);
        LOCAL marketplaceWithheldTax = NUMERIC[14,2](INTEGER);
        LOCAL sellingFees = NUMERIC[14,2](INTEGER);
        LOCAL fbaFees = NUMERIC[14,2](INTEGER);
        LOCAL otherTransactionFees = NUMERIC[14,2](INTEGER);
        LOCAL other = NUMERIC[14,2](INTEGER);
        LOCAL otherStr = STRING(INTEGER);
        LOCAL totalStr = STRING (INTEGER);
        LOCAL total = NUMERIC[14,2](INTEGER);
        LOCAL item = Item(INTEGER);
        LOCAL index = INTEGER(INTEGER);
        
        getValuesRange(idTable(t), lpad(STRING(extractMonthNumber(date)),2,'0') + '!A9:AA', defaultAccount());
        valuesRows(INTEGER i) <- replace(replace(replace(replace(replace(valuesRows(i), ';', ''), '[', ''), ']', ''), '",', ';'), '"', '');
        str() <- GROUP CONCAT valuesRows(INTEGER i), '\n' ORDER i;
        str() <- IF countryDivideSymbol(t) != '.' OR NOT countryDivideSymbol(t) THEN replace(str(), '.', '') ELSE str();
        stringToFile(str());
        
        IF useShortFormat(t) THEN{
            IMPORT CSV ';' NOHEADER FROM resultFile() TO dateTime = A, type = C, reference = D, model = E, quantity = G,
                    productSalesStr = M, 
                    postageCredits = N,
                    giftWrapCredits = O,
                    promotionalRebates = P,
                    productSalesTax = Q,
                    marketplaceWithheldTax = R, sellingFees = S, fbaFees = T,
                    otherTransactionFees = U, otherStr = V, totalStr = W;
        } ELSE{
            IMPORT CSV ';' NOHEADER FROM resultFile() TO dateTime = A, type = C, reference = D, model = E, quantity = G,
                    productSalesStr = N, productSalesTax = O, postageCredits = P,
                    shippingCreditsTax = Q, giftWrapCredits = R, giftwrapCreditsTax = S,
                    promotionalRebates = T, promotionalRebatesTax = U,
                    marketplaceWithheldTax = V, sellingFees = W, fbaFees = X,
                    otherTransactionFees = Y, otherStr = Z, totalStr = AA;
        }
        
        item(INTEGER i) <- (OVERRIDE item(otherAccount(type(i), t)), productModel(model(i)));
        dateTimeExt(INTEGER i) <- toDateFormat(dateTime(i), OVERRIDE dateFormat(t), 'MM/dd/yy');
        dateTimeExt(INTEGER i) <- IF dateTimeExt(i) < firstDayOfMonth(date) THEN firstDayOfMonth(date) ELSE dateTimeExt(i);
        
        FOR INTEGER x = [GROUP MAX INTEGER i IF model(i) AND NOT item(i) BY dateTime(i)](STRING dt) DO{
            error(type(x) + ' : ' + dt, t) <- CONCAT ' : ', 'Не найден sku ' + model(x), dt, type(x);
        }
        FOR INTEGER x = [GROUP MAX INTEGER i IF type(i) != typeOrder(t) AND type(i) != typeReturn(t) AND NOT otherAccount(type(i), t) BY dateTime(i)](STRING dt) DO{
            error(type(x) + ' : ' + dt, t) <- CONCAT ' : ', 'Не найден статус ' + type(x), dt, model(x);
        }
        
        IF (GROUP SUM 1 IF error(STRING s, t)) THEN {
            MESSAGE 'По таблице есть ошибки, импорт не возможен';
            APPLY;
            RETURN;
        }
        referenceExt (INTEGER i) <- CASE 
                                        WHEN reference(i) THEN reference(i)
                                        WHEN otherAccount(type(i), t) AND NOT reference(i) THEN dateTime(i)
        ;
        
        quantityExt(INTEGER i) <- CASE 
                                      WHEN type(i) = typeOrder(t) THEN quantity(i)
                                      WHEN type(i) = typeReturn(t) THEN -quantity(i)
                                      WHEN otherAccount(type(i), t) THEN 1
        ;
        index(INTEGER i) <- PARTITION SUM 1 ORDER i BY referenceExt(i), item(i);
        total(INTEGER i) <- parseSum2(productSalesStr(i), t);
        other(INTEGER i) <- parseSum2(otherStr(i), t);
        FOR otherStr(INTEGER  i)  DO{
            printToLog(CONCAT ' : ', otherStr(i), other(i), '1 ' + parseSum(otherStr(i), t), '2 ' + parseSum2(otherStr(i), t));
        }
//        NUMERIC[14,2](replace((IF NOT countryDivideSymbol(t) != '.'  OR NOT countryDivideSymbol(t) THEN replace(productSalesStr(i), '.', '') ELSE productSalesStr(i)), ' ', ''));

//        total(INTEGER i) <- IF productSales(i) = 0 THEN NUMERIC[14,2](replace(totalStr(i), '.', '')) 
//            ELSE productSales(i);
        
        FOR [GROUP MAX INTEGER i BY dateTimeExt(i)](DATE d) AND NOT invoice(t, d) DO NEW i = Invoice{
            dateTime(i) <- dateTimeToDateTime(d, TIME('23:59:59'));
            type(i) <- amazonInvoiceType();
            customer(i) <- FBAPartner();
            location(i) <- location('MX000004');
            ready(i) <- TRUE;
            country(i) <- country(t);
            marketplace(i) <- marketplace(t);
            eStore(i) <- eStore(t);
        }

        FOR INTEGER xx = [GROUP MAX INTEGER i BY dateTimeExt(i)](DATE d) AND Invoice i = invoice(t, d) DO{
            
            LOCAL part = INTEGER(InvoiceLine);
            part(InvoiceLine l) <- PARTITION SUM 1 IF invoice(l) == i ORDER index(l), l BY invoice(l), customerReference(l), item(l);

            FOR [GROUP MAX INTEGER j BY referenceExt(j), item(j), index(j), dateTimeExt(j)](STRING[30] ref, Item item, INTEGER index, d) AND NOT [GROUP MAX InvoiceLine l IF invoice(l) == i BY customerReference(l), item(l), part(l), date(l)](ref,item,index, d) NEW l = InvoiceLine DO {
                invoice(l) <- i;
                customerReference(l) <- ref;
                item(l) <- item;
                part(l) <- index;
            }
            
            FOR INTEGER x = [GROUP MAX INTEGER j BY referenceExt(j), item(j), index(j), dateTimeExt(j)](STRING[30] ref, Item item, INTEGER index, d) AND InvoiceLine il == [GROUP MAX InvoiceLine l IF invoice(l) == i BY customerReference(l), item(l), part(l), date(l)](ref,item,index, d) DO {
                quantity(il) <- quantityExt(x);
                untaxedAmount(il) <- total(x) IF total(x) > 0;
                price(il) <- NUMERIC[10,2](total(x)/quantityExt(x));
                
                in(il, Tax tt) <- NULL;
                taxAmount(Tax tt, il) <- NULL;
                IF productSalesTax(x) > 0 OR productSalesTax(x) < 0 THEN {
                    in(il, Tax tt) <- TRUE WHERE name(tt) == 'Налог на продажу товаров';
                    taxAmount(Tax tt, il) <- -productSalesTax(x) WHERE name(tt) == 'Налог на продажу товаров';
                }
                IF shippingCreditsTax(x) > 0 OR shippingCreditsTax(x) < 0 THEN {
                    in(il, Tax tt) <- TRUE WHERE name(tt) == 'Налог на кредит на доставку';
                    taxAmount(Tax tt, il) <- -shippingCreditsTax(x) WHERE name(tt) == 'Налог на кредит на доставку';
                }
                IF giftwrapCreditsTax(x) > 0 OR giftwrapCreditsTax(x) < 0 THEN {
                    in(il, Tax tt) <- TRUE WHERE name(tt) == 'Налог на подарочную упаковку';
                    taxAmount(Tax tt, il) <- -giftwrapCreditsTax(x) WHERE name(tt) == 'Налог на подарочную упаковку';
                }
                IF promotionalRebatesTax(x) > 0 OR promotionalRebatesTax(x) < 0 THEN {
                    in(il, Tax tt) <- TRUE WHERE name(tt) == 'Налог на рекламные скидки';
                    taxAmount(Tax tt, il) <- -promotionalRebatesTax(x) WHERE name(tt) == 'Налог на рекламные скидки';
                }
                IF marketplaceWithheldTax(x) > 0 OR marketplaceWithheldTax(x) < 0 THEN {
                    in(il, Tax tt) <- TRUE WHERE name(tt) == 'Налог, удерживаемый на рынке';
                    taxAmount(Tax tt, il) <- -marketplaceWithheldTax(x) WHERE name(tt) == 'Налог, удерживаемый на рынке';
                }

                dataAmount(il, CustomSalesAccount aa) <- NULL;
                IF postageCredits(x) > 0 OR postageCredits(x) < 0 THEN {
                    dataAmount(il, CustomSalesAccount aa) <- -postageCredits(x) WHERE id(aa) == 'postage_credits';
                }
                IF giftWrapCredits(x) > 0 OR giftWrapCredits(x) < 0 THEN {
                    dataAmount(il, CustomSalesAccount aa) <- -giftWrapCredits(x) WHERE id(aa) == 'gift_wrap_credits';
                }
                IF promotionalRebates(x) > 0 OR promotionalRebates(x) < 0 THEN {
                    dataAmount(il, CustomSalesAccount aa) <- -promotionalRebates(x) WHERE id(aa) == 'promotional_rebates';
                }
                IF sellingFees(x) > 0 OR sellingFees(x) < 0 THEN {
                    dataAmount(il, CustomSalesAccount aa) <- -sellingFees(x) WHERE id(aa) == 'selling_fees';
                }
                IF fbaFees(x) > 0 OR fbaFees(x) < 0 THEN {
                    dataAmount(il, CustomSalesAccount aa) <- -fbaFees(x) WHERE id(aa) == 'fba_fees';
                }
                IF otherTransactionFees(x) > 0 OR otherTransactionFees(x) < 0 THEN {
                    dataAmount(il, CustomSalesAccount aa) <- -otherTransactionFees(x) WHERE id(aa) == 'other_transaction_fees';
                }
                IF other(x) > 0 OR other(x) < 0 THEN {
                    dataAmount(il, CustomSalesAccount aa) <- -other(x) WHERE id(aa) == 'other';
                }
                FOR OtherAccount oa = otherAccount(type(x), t) AND item(x) != item(oa) DO
                    dataAmount(il, oa) <- -parseSum2(totalStr(x), t);
//                        NUMERIC[14,2](replace(totalStr(x), '.', ''));
            }
        }
        APPLY;
    } 
}

importInvoiceAmazon 'Импорт реализации с Амазон' (ImportTable t, DATE date){
    importInvoiceAmazon(t, date, NULL);
}

importInvoiceAmazon 'Импорт реализации с Амазон' (ImportTable t) {
    DIALOG dialogDate OBJECTS d INPUT DO {
        IF marketplace(t) = Marketplace.amazon THEN importInvoiceAmazon(t, d);
        ELSE MESSAGE 'Только для Amazon';
    }
}

importInvoiceAmazon 'Импорт реализации с Амазон' () {
    DIALOG dialogDate OBJECTS d INPUT DO {
        FOR active(ImportTable t) AND marketplace(t) = Marketplace.amazon DO {
            importInvoiceAmazon(t, d);
        }
    }
}

EXTEND FORM Sales.options
    OBJECTS it = ImportTable
    PROPERTIES (it) nameMarketplace, nameCountry, nameEStore, idTable, typeOrder, typeReturn, active FIRST 
    PROPERTIES(it) TOOLBAR importInvoiceAmazon, importProductsAmazon
    PROPERTIES (it) NEW, EDIT, DELETE
    PROPERTIES() nameAmazonInvoiceType, importInvoiceAmazon
    
    OBJECTS error = STRING
    PROPERTIES READONLY 'Ошибка' = VALUE(error), error(error, it)
    FILTERS error(error, it)
    
    
    OBJECTS oa = OtherAccount
    PROPERTIES caption(oa, it) COLUMNS (oa) HEADER name(oa) GRID DRAW it
    
;
DESIGN Sales.options{
    tabbedPane{
        NEW import{
            caption = 'Импорт';
            MOVE BOX (it);
            MOVE BOX (error);
            MOVE PROPERTY (nameAmazonInvoiceType());
            MOVE PROPERTY (importInvoiceAmazon());
            
        }
    }
}

onStarted() + {
    IF NOT (GROUP MAX Tax tt IF name(tt) == 'Налог на продажу товаров') THEN NEW t = Tax {
        name(t) <- 'Налог на продажу товаров';
    }
    IF NOT (GROUP MAX Tax tt IF name(tt) == 'Налог на кредит на доставку') THEN NEW t = Tax {
        name(t) <- 'Налог на кредит на доставку';
    }
    IF NOT (GROUP MAX Tax tt IF name(tt) == 'Налог на подарочную упаковку') THEN NEW t = Tax {
        name(t) <- 'Налог на подарочную упаковку';
    }
    IF NOT (GROUP MAX Tax tt IF name(tt) == 'Налог на рекламные скидки') THEN NEW t = Tax {
        name(t) <- 'Налог на рекламные скидки';
    }
    IF NOT (GROUP MAX Tax tt IF name(tt) == 'Налог, удерживаемый на рынке') THEN NEW t = Tax {
        name(t) <- 'Налог, удерживаемый на рынке';
    }
    IF NOT (GROUP MAX CustomSalesAccount aa IF id(aa) == 'postage_credits') THEN NEW a = CustomSalesAccount {
        id(a) <- 'postage_credits';
        name(a) <- 'Кредит на расходы по доставке';
    }
    IF NOT (GROUP MAX CustomSalesAccount aa IF id(aa) == 'gift_wrap_credits') THEN NEW a = CustomSalesAccount {
        id(a) <- 'gift_wrap_credits';
        name(a) <- 'Кредит на подарочную упаковку';
    }
    IF NOT (GROUP MAX CustomSalesAccount aa IF id(aa) == 'promotional_rebates') THEN NEW a = CustomSalesAccount {
        id(a) <- 'promotional_rebates';
        name(a) <- 'Скидки в рамках рекламных акций';
    }
    IF NOT (GROUP MAX CustomSalesAccount aa IF id(aa) == 'selling_fees') THEN NEW a = CustomSalesAccount {
        id(a) <- 'selling_fees';
        name(a) <- 'Сборы с продаж';
    }
    IF NOT (GROUP MAX CustomSalesAccount aa IF id(aa) == 'fba_fees') THEN NEW a = CustomSalesAccount {
        id(a) <- 'fba_fees';
        name(a) <- 'Сборы за доставку через Amazon';
    }
    IF NOT (GROUP MAX CustomSalesAccount aa IF id(aa) == 'other_transaction_fees') THEN NEW a = CustomSalesAccount {
        id(a) <- 'other_transaction_fees';
        name(a) <- 'Другие сборы за транзакции';
    }
    IF NOT (GROUP MAX CustomSalesAccount aa IF id(aa) == 'other') THEN NEW a = CustomSalesAccount {
        id(a) <- 'other';
        name(a) <- 'Другие';
    }
}

ebayInvoiceType = DATA InvoiceType ();
nameEbayInvoiceType 'Тип реализации для eBay' = name(ebayInvoiceType());

//импорт реализации с eBay
activeImportInvoiceEbay '{Active}' = DATA BOOLEAN (Country);
idImportInvoiceEbay 'ID таблицы' = DATA STRING (Country) CHARWIDTH 40;
typeImportInvoiceEbay 'Тип зказа' = DATA STRING (Country) CHARWIDTH 20;

CONSTRAINT (SET(activeImportInvoiceEbay(Country c)) OR CHANGED( idImportInvoiceEbay(c)) OR CHANGED(typeImportInvoiceEbay(c)))
    AND activeImportInvoiceEbay(c) AND NOT (idImportInvoiceEbay(c) AND typeImportInvoiceEbay(c))
    MESSAGE 'Не заполнены ID таблицы и Тип заказа';

importProductsEbay 'Импорт товаров с eBay' (ImportTable t){
    importProducts(idTable(t), 'SKU!A2:B', categoryByName('Телефоны'));
    importProducts(idTable(t), 'SKU!D2:E', categoryByName('Здоровье'));
    importProducts(idTable(t), 'SKU!G2:H', categoryByName('Аксессуары'));
}

invoiceEbay (DATE d, ImportTable t) = GROUP LAST Invoice ii IF date(ii) == d AND type(ii) == ebayInvoiceType() 
    AND customer(ii) == FBAPartner() AND eStore(ii) = eStore(t) AND country(ii) = country(t) ORDER dateTime(ii), ii;

importInvoiceEbay 'Импорт реализации с eBay' (ImportTable t, DATE date, BOOLEAN skipImportProducts) {
    IF NOT skipImportProducts THEN  importProductsEbay(t);

    NEWSESSION {
        getValuesRange(idTable(t), 'SKU!A2:B', defaultAccount());
        valuesRows(INTEGER i) <- replace(replace(replace(replace(valuesRows(i), ';', ''), '[', ''), ']', ''), '",', '";');
        LOCAL str = STRING();
        str() <- GROUP CONCAT valuesRows(INTEGER i), '\n' ORDER i;
        stringToFile(str());

        LOCAL amzModel = STRING(INTEGER);
        LOCAL baseModel = STRING(INTEGER);

        IMPORT CSV ';' NOHEADER FROM resultFile() TO amzModel, baseModel;

        LOCAL date = DATE(INTEGER);
        LOCAL type = STRING(INTEGER);
        LOCAL reference = STRING(INTEGER);
        LOCAL referenceExt = STRING(INTEGER);
        LOCAL article = STRING(INTEGER);
        LOCAL model = STRING(INTEGER);
        LOCAL quantity = NUMERIC[16,3](INTEGER);
        LOCAL quantityExt = NUMERIC[16,3](INTEGER);
        LOCAL sum = NUMERIC[14,2](INTEGER);
        LOCAL productSales = NUMERIC[14,2](INTEGER);
        LOCAL productSalesStr = STRING(INTEGER);
        LOCAL packagingAndShipping = NUMERIC[14,2](INTEGER);
        LOCAL sellerTax = NUMERIC[14,2](INTEGER);
        LOCAL ebayTax = NUMERIC[14,2](INTEGER);
        LOCAL fixedSalesCommission = NUMERIC[14,2](INTEGER);
        LOCAL variableSalesCommission = NUMERIC[14,2](INTEGER);
        LOCAL highRateOfNotDescribedItemsFee = NUMERIC[14,2](INTEGER);
        LOCAL belowAverageServiceFees = NUMERIC[14,2](INTEGER);
        LOCAL internationalFees = NUMERIC[14,2](INTEGER);
        LOCAL charityDonation = NUMERIC[14,2](INTEGER);
        LOCAL total = NUMERIC[14,2](INTEGER);
        LOCAL totalStr = STRING(INTEGER);
        LOCAL item = Item(INTEGER);
        LOCAL index = INTEGER(INTEGER);

        getValuesRange(idTable(t), lpad(STRING(extractMonthNumber(date)),2,'0') + '!A2:AH', defaultAccount());
        valuesRows(INTEGER i) <- replace(replace(replace(replace(replace(valuesRows(i), ';', ''), '[', ''), ']', ''), '",', ';'), '"', '');
        str() <- GROUP CONCAT valuesRows(INTEGER i), '\n' ORDER i;
        stringToFile(str());

        IMPORT CSV ';' NOHEADER FROM resultFile() TO date = A, type = B, referenceExt = C, article = R, model = U, quantity = V,
                                                     sum = W, totalStr = K, packagingAndShipping = X, sellerTax = Y,
                                                     ebayTax = Z, fixedSalesCommission = AB, variableSalesCommission = AC,
                                                     highRateOfNotDescribedItemsFee = AD, belowAverageServiceFees = AE,
                                                     internationalFees = AF, charityDonation = AG, productSalesStr = AH;

        item(INTEGER i) <- (OVERRIDE item(otherAccount(type(i), t)), productModel(model(i)));
        reference (INTEGER i) <- CASE
            WHEN referenceExt(i) AND referenceExt(i) != '--' THEN referenceExt(i)
            WHEN otherAccount(type(i), t) AND referenceExt(i) = '--' THEN i + '_' + date(i)
        ;

        quantityExt(INTEGER i) <- CASE
            WHEN type(i) = typeOrder(t) THEN quantity(i)
            WHEN type(i) = typeReturn(t) THEN -quantity(i)
            WHEN otherAccount(type(i), t) THEN 1
        ;
        //        item(INTEGER i) <- productModel((GROUP MAX baseModel(INTEGER j) IF amzModel(j) == model(i))) IF type(i) == typeOrder(t);
        index(INTEGER i) <- PARTITION SUM 1 ORDER i BY reference(i), item(i);
        total(INTEGER i) <- NUMERIC[14,2]((replace(productSalesStr(i), '--', '')));
        
        FOR INTEGER x = [GROUP MAX INTEGER ii BY date(ii)](DATE d) AND NOT invoiceEbay(d, t) NEW i = Invoice DO{
            dateTime(i) <- dateTimeToDateTime(d, TIME('23:59:59'));
            type(i) <- ebayInvoiceType();
            customer(i) <- FBAPartner();
            location(i) <- location('MX000004');
            country(i) <- country(t);
            marketplace(i) <- marketplace(t);
            eStore(i) <- eStore(t);
            ready(i) <- TRUE ;
        }
        FOR [GROUP MAX INTEGER ii BY date(ii)](DATE d) AND Invoice i = invoiceEbay(d, t) DO{
            LOCAL part = INTEGER(InvoiceLine);
            part(InvoiceLine l) <- PARTITION SUM 1 IF invoice(l) == i ORDER index(l), l BY invoice(l), customerReference(l), item(l);

            
            FOR [GROUP MAX INTEGER j IF date(j) = d BY reference(j), item(j), index(j)](STRING[30] ref, Item item, INTEGER index) 
                AND NOT [GROUP MAX InvoiceLine l IF invoice(l) == i BY customerReference(l), item(l), part(l), date(l)](ref,item,index, d) NEW l = InvoiceLine DO {
                invoice(l) <- i;
                customerReference(l) <- ref;
                item(l) <- item;
            }

            FOR INTEGER x = [GROUP MAX INTEGER j IF date(j) = d BY reference(j), item(j), index(j), date(j)](STRING[30] ref, Item item, INTEGER index, d) AND InvoiceLine il == [GROUP MAX InvoiceLine l IF invoice(l) == i BY customerReference(l), item(l), part(l), date(l)](ref,item,index,d) DO {
                quantity(il) <- quantityExt(x);
                untaxedAmount(il) <- total(x);
                price(il) <- NUMERIC[10,2](total(x)/quantityExt(x));

                in(il, Tax tt) <- NULL;
                taxAmount(Tax tt, il) <- NULL;
                IF sellerTax(x) > 0 OR sellerTax(x) < 0 THEN {
                    in(il, Tax tt) <- TRUE WHERE name(tt) == 'Налог, взимаемый продавцом';
                    taxAmount(Tax tt, il) <- -sellerTax(x) WHERE name(tt) == 'Налог, взимаемый продавцом';
                }
                IF ebayTax(x) > 0 OR ebayTax(x) < 0 THEN {
                    in(il, Tax tt) <- TRUE WHERE name(tt) == 'Налог, взимаемый eBay';
                    taxAmount(Tax tt, il) <- -ebayTax(x) WHERE name(tt) == 'Налог, взимаемый eBay';
                }
                IF internationalFees(x) > 0 OR internationalFees(x) < 0 THEN {
                    in(il, Tax tt) <- TRUE WHERE name(tt) == 'Международный сбор';
                    taxAmount(Tax tt, il) <- -internationalFees(x) WHERE name(tt) == 'Международный сбор';
                }

                dataAmount(il, CustomSalesAccount aa) <- NULL;
                IF packagingAndShipping(x) > 0 OR packagingAndShipping(x) < 0 THEN {
                    dataAmount(il, CustomSalesAccount aa) <- -packagingAndShipping(x) WHERE id(aa) == 'packaging_and_shipping';
                }
                IF fixedSalesCommission(x) > 0 OR fixedSalesCommission(x) < 0 THEN {
                    dataAmount(il, CustomSalesAccount aa) <- -fixedSalesCommission(x) WHERE id(aa) == 'fixed_sales_commission';
                }
                IF variableSalesCommission(x) > 0 OR variableSalesCommission(x) < 0 THEN {
                    dataAmount(il, CustomSalesAccount aa) <- -variableSalesCommission(x) WHERE id(aa) == 'variable_sales_commission';
                }
                IF highRateOfNotDescribedItemsFee(x) > 0 OR highRateOfNotDescribedItemsFee(x) < 0 THEN {
                    dataAmount(il, CustomSalesAccount aa) <- -highRateOfNotDescribedItemsFee(x) WHERE id(aa) == 'high_rate_of_not_described_items_fees';
                }
                IF belowAverageServiceFees(x) > 0 OR belowAverageServiceFees(x) < 0 THEN {
                    dataAmount(il, CustomSalesAccount aa) <- -belowAverageServiceFees(x) WHERE id(aa) == 'below_average_service_fees';
                }
                IF charityDonation(x) > 0 OR charityDonation(x) < 0 THEN {
                    dataAmount(il, CustomSalesAccount aa) <- -charityDonation(x) WHERE id(aa) == 'charity_donation';
                }
                LOCAL other = NUMERIC[14,2]();
                other() <- (GROUP SUM total(INTEGER j) IF date(j) == date(x) AND reference(j) == reference(x) AND article(j) == article(x) AND type(j) == 'Andere Gebühr');
                IF other() > 0 OR other() < 0 THEN {
                    dataAmount(il, CustomSalesAccount aa) <- other() WHERE id(aa) == 'other_fees';
                }
                FOR OtherAccount oa = otherAccount(type(x), t) AND item(x) != item(oa) DO
                    dataAmount(il, oa) <- -NUMERIC[14,2](total(x));
            }
        }
        APPLY;
    }
}

importInvoiceEbay 'Импорт реализации с eBay' (ImportTable t, DATE date) {
    importInvoiceEbay(t, date, NULL);
}

importInvoiceEbay 'Импорт реализации с eBay' (ImportTable t) {
    DIALOG dialogDate OBJECTS d INPUT DO {
        IF marketplace(t) = Marketplace.ebay THEN importInvoiceEbay(t, d);
        ELSE MESSAGE 'Только для ebay';
    }
}

importInvoiceEbay 'Импорт реализации с eBay' () {
    DIALOG dialogDate OBJECTS d INPUT DO {
        FOR active(ImportTable t) AND marketplace(t) = Marketplace.ebay DO {
            importInvoiceEbay(t, d);
        }
    }
}

EXTEND FORM Sales.options
    PROPERTIES(it) TOOLBAR importInvoiceEbay
    PROPERTIES() nameEbayInvoiceType, importInvoiceEbay
;
DESIGN Sales.options{
    import{
        MOVE PROPERTY(nameEbayInvoiceType());
        MOVE PROPERTY(importInvoiceEbay());
    }
}

onStarted() + {
    IF NOT (GROUP MAX Tax tt IF name(tt) == 'Налог, взимаемый продавцом') THEN NEW t = Tax {
        name(t) <- 'Налог, взимаемый продавцом';
    }
    IF NOT (GROUP MAX Tax tt IF name(tt) == 'Налог, взимаемый eBay') THEN NEW t = Tax {
        name(t) <- 'Налог, взимаемый eBay';
    }
    IF NOT (GROUP MAX Tax tt IF name(tt) == 'Международный сбор') THEN NEW t = Tax {
        name(t) <- 'Международный сбор';
    }
    IF NOT (GROUP MAX CustomSalesAccount aa IF id(aa) == 'packaging_and_shipping') THEN NEW a = CustomSalesAccount {
        id(a) <- 'packaging_and_shipping';
        name(a) <- 'Упаковка и доставка';
    }
    IF NOT (GROUP MAX CustomSalesAccount aa IF id(aa) == 'fixed_sales_commission') THEN NEW a = CustomSalesAccount {
        id(a) <- 'fixed_sales_commission';
        name(a) <- 'Фиксированная часть комиссии за продажу';
    }
    IF NOT (GROUP MAX CustomSalesAccount aa IF id(aa) == 'variable_sales_commission') THEN NEW a = CustomSalesAccount {
        id(a) <- 'variable_sales_commission';
        name(a) <- 'Переменная часть комиссии за продажу';
    }
    IF NOT (GROUP MAX CustomSalesAccount aa IF id(aa) == 'high_rate_of_not_described_items_fees') THEN NEW a = CustomSalesAccount {
        id(a) <- 'high_rate_of_not_described_items_fees';
        name(a) <- 'Вознаграждение за очень высокий процент "товаров, не соответствующих описанию"';
    }
    IF NOT (GROUP MAX CustomSalesAccount aa IF id(aa) == 'below_average_service_fees') THEN NEW a = CustomSalesAccount {
        id(a) <- 'below_average_service_fees';
        name(a) <- 'Плата за обслуживание на уровне ниже среднего';
    }
    IF NOT (GROUP MAX CustomSalesAccount aa IF id(aa) == 'charity_donation') THEN NEW a = CustomSalesAccount {
        id(a) <- 'charity_donation';
        name(a) <- 'Благотворительное пожертвование';
    }
    IF NOT (GROUP MAX CustomSalesAccount aa IF id(aa) == 'other_fees') THEN NEW a = CustomSalesAccount {
        id(a) <- 'other_fees';
        name(a) <- 'Другая плата';
    }
}

onStarted()+{
    IF NOT categoryByName('Телефоны') THEN NEW cc = Category{
        name(cc) <- 'Телефоны';
        parent(cc) <- GROUP MIN Category c IF name(c) == 'Все';
    }
    IF NOT categoryByName('Здоровье') THEN NEW cc = Category{
        name(cc) <- 'Здоровье';
        parent(cc) <- GROUP MIN Category c IF name(c) == 'Все';
    }
    IF NOT categoryByName('Аксессуары') THEN NEW cc = Category{
        name(cc) <- 'Аксессуары';
        parent(cc) <- GROUP MIN Category c IF name(c) == 'Все';
    }
}