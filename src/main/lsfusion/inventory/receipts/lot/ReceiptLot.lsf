MODULE ReceiptLot;

REQUIRE ProductLot, ReceiptSearch, ReceiptInv, InvLedgerLot;

NAMESPACE Inventory;

@defineDocLot (receipt, product, done, done, ' (done)');

@defineDocBarCodeLot (receipt, product, done);

beforeProcessReceiptLineLotBarCode(Receipt r, STRING s) + {
    IF NOT lot(s) THEN {
        NEW l = Lot {
            id(l) <- STRING[100](s);
        }
    }
}

initialDemand 'Initial demand' = DATA NUMERIC[16,3] (ReceiptLine, Lot);
initialDemandLot 'Lot initial demand' (ReceiptLine rl) = GROUP SUM initialDemand(rl, Lot l);
initialDemandLot 'Lot initial demand' (Receipt r) = GROUP SUM initialDemand(ReceiptLine rl, Lot l) IF receipt(rl) = r;

initialDemandOrDone(ReceiptLine sl, Lot l) = initialDemand(sl, l) OR done(sl, l);
@defineDocDesignLotTab(receipt, ReceiptLine, done, product, l, linesTab, initialDemandOrDone);

@defineDocLotGenerate(receipt, product, done);

EXTEND FORM receipt 
    PROPERTIES (l, lotL) BEFORE done(l, lotL) initialDemand
    PROPERTIES (lotL) DELETE GRID
;

//ledger
ledgerDone = ABSTRACT NUMERIC[16,3] (ReceiptLine, Lot);
quantity(ReceiptInvLedger rl, Lot l) += done(line(rl), l) (-) ledgerDone(line(rl), l);